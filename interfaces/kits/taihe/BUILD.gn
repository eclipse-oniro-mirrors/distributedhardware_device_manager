# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//foundation/distributedhardware/device_manager/device_manager.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")

subsystem_name = "distributedhardware"
part_name = "device_manager"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name"

copy_taihe_idl("copy_taihe") {
  sources = [ "${devicemanager_path}/interfaces/kits/taihe/idl/ohos.distributedDeviceManager.taihe" ]
}

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_taihe" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.distributedDeviceManager.ani.cpp",
    "$taihe_generated_file_path/src/ohos.distributedDeviceManager.abi.c",
  ]
}

taihe_shared_library("device_manager_ani_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  subsystem_name = "$subsystem_name"
  part_name = "$part_name"

  sanitize = {
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
    integer_overflow = true
    ubsan = true
  }

  include_dirs = [
    "include",
    "${innerkits_path}/native_cpp/include",
    "${devicemanager_path}/interfaces/kits/taihe/include",
  ]

  sources = get_target_outputs(":run_taihe")
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.distributedDeviceManager.cpp",
  ]

  deps = [
    ":run_taihe",
    "${innerkits_path}/native_cpp:devicemanagersdk"
  ]

  external_deps = [
    "hilog:libhilog",
  ]
}

generate_static_abc("ohos_distributedDeviceManager_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.distributedDeviceManager.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/ohos_distributedDeviceManager_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("ohos_distributedDeviceManager_etc") {
  source = "$target_out_dir/ohos_distributedDeviceManager_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [
    ":ohos_distributedDeviceManager_abc"
  ]
}

group("devicemanager_ani") {
  deps = [
    ":device_manager_ani_taihe",
    ":ohos_distributedDeviceManager_etc",
  ]
}