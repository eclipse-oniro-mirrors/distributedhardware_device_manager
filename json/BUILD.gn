# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
} else {
  import("//build/ohos.gni")
}
import("//foundation/distributedhardware/device_manager/device_manager.gni")

if (defined(ohos_lite)) {
  if (ohos_kernel_type == "linux") {
    shared_library("devicemanagerjson") {
      include_dirs = [
        "include",
        "${common_path}/include",
      ]

      if (use_nlohmann_json) {
        include_dirs += [ "//third_party/json/include" ]
      } else {
        include_dirs += [ "//third_party/cJSON" ]
      }

      if (use_nlohmann_json) {
        sources = [ "src/json_object_nlohmannjson.cpp" ]
      } else {
        sources = [ "src/json_object_cjson.cpp" ]
      }

      defines = [
        "HI_LOG_ENABLE",
        "DH_LOG_TAG=\"devicemanagerjson\"",
        "LOG_DOMAIN=0xD004110",
      ]

      deps = [ "${hilog_path}:hilog_shared" ]
    }
  }
} else {
  config("cflags_config") {
    cflags = [
      "-Werror",
      "-fPIC",
      "-fstack-protector-strong",
      "-ffunction-sections",
      "-fdata-sections",
      "-Oz",
      "-flto",
    ]
    ldflags = [ "-flto" ]
  }

  ohos_shared_library("devicemanagerjson") {
    branch_protector_ret = "pac_ret"

    include_dirs = [
      "include",
      "${common_path}/include",
    ]

    sanitize = {
      cfi = true
      cfi_cross_dso = true
      debug = false
    }

    cflags = [
      "-Werror",
      "-fPIC",
      "-fstack-protector-strong",
    ]

    ldflags = [
      "-Wl,-z,relro",
      "-Wl,-z,now",
    ]

    configs = [ ":cflags_config" ]

    if (use_nlohmann_json) {
      sources = [ "src/json_object_nlohmannjson.cpp" ]
    } else {
      sources = [ "src/json_object_cjson.cpp" ]
    }

    defines = [
      "HI_LOG_ENABLE",
      "DH_LOG_TAG=\"devicemanagerjson\"",
      "LOG_DOMAIN=0xD004110",
    ]

    deps = []

    external_deps = [ "hilog:libhilog" ]

    if (use_nlohmann_json) {
      external_deps += [ "json:nlohmann_json_static" ]
    } else {
      external_deps += [ "cJSON:cjson" ]
    }

    subsystem_name = "distributedhardware"

    part_name = "device_manager"
  }
}
